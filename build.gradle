plugins {
    id 'java'
    id 'maven-publish'
    id 'maven'
    id 'java-gradle-plugin'
    id 'idea'
    id 'eclipse'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'groovy'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'io.github.badlionmoddinggroup'
version '2.4.0'

repositories {
    maven {
        name = "MinecraftForge"
        url = uri("https://files.minecraftforge.net/maven/")
    }
    maven {
        name = "Fabric"
        url = 'https://maven.fabricmc.net/'
    }
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

dependencies {
    implementation gradleApi()

    // libraries
    implementation('commons-io:commons-io:2.6')
    implementation('org.zeroturnaround:zt-zip:1.13')
    implementation('com.google.code.gson:gson:2.8.5')
    implementation('com.google.guava:guava:28.0-jre')
    implementation 'com.github.kylestev:xdeltaencoder:v1.0.1'

    // game handling utils
    implementation('net.fabricmc:stitch:0.4.6+build.74') {
        exclude module: 'enigma'
    }

    // tinyfile management
    implementation('net.fabricmc:tiny-mappings-parser:0.2.2.14')

    implementation('net.fabricmc:lorenz-tiny:2.0.0+build.2') {
        transitive = false
    }

    // decompilers
    implementation('net.fabricmc:procyon-fabric-compilertools:0.5.35.13')
    implementation("net.minecraftforge:forgeflower:1.5.478.15")

    // source code remapping
    implementation('org.cadixdev:mercury:0.1.0.fabric-SNAPSHOT')

    // enigma
    implementation('cuchaz:enigma-swing:0.20+build.206')
    implementation('cuchaz:enigma-cli:0.20+build.206')

    implementation 'org.ow2.asm:asm:8.0'
    implementation 'org.ow2.asm:asm-commons:8.0'
    implementation 'org.ow2.asm:asm-tree:8.0'
    implementation 'org.ow2.asm:asm-util:8.0'
}

gradlePlugin {
    plugins {
        plugin {
            id = 'badlion-gradle'
            implementationClass = 'io.github.badlionmoddinggroup.badliongradle.BadlionGradle'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact(file("${project.buildDir}/libs/${project.archivesBaseName}-${project.version}.jar")) {
                builtBy build
            }
            groupId 'io.github.badlionmoddinggroup'
            artifactId 'badliongradle'
            version = project.version
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

bintray {
    user = "hydos"
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')
    publications = ["maven"]
    publish = true
    pkg {
        repo = "bmg"
        name = "badliongradle"
        userOrg = "badlion"
        licenses = ["Apache-2"]
        version {
            name = "BadlionGradle"
            released = new Date()
            desc = "Gradle plugin for managing badlion modding stuff"
            githubRepo = 'BadlionModdingGroup/BadlionGradle'
            websiteUrl = 'https://github.com/BadlionModdingGroup'
            issueTrackerUrl = 'https://github.com/BadlionModdingGroup/BadlionGradle/issues'
            vcsUrl = 'https://github.com/BadlionModdingGroup/BadlionGradle/BadlionGradle.git'
            gpg {
                sign = true
            }
            attributes = [
                    'gradle-plugin': 'io.github.badlionmoddinggroup:io.github.badlionmoddinggroup:gradle-badlion-plugin'
            ]
        }
    }
}
